--
-- CSC 322 Project 3
--
-- Name:Wenzhe Shi
-- Vnum:V00968344
-- Name: Vyom Shah
-- Vnum: V00951024
--
MODULE main
  
VAR
  chopsticks : array 0..4 of {nobody,0,1,2,3,4}; -- who holds the chopstick.
  -- the token to eat
  token : {0,1,2,3,4};
  --
  -- Philosopher i has chopstick i on left and chopstick
  -- (i + 1) mod 5 on right
  --
  p0 : process philosopher(0,chopsticks[0],chopsticks[1], token);
  p1 : process philosopher(1,chopsticks[1],chopsticks[2], token);
  p2 : process philosopher(2,chopsticks[2],chopsticks[3], token);
  p3 : process philosopher(3,chopsticks[3],chopsticks[4], token);
  p4 : process philosopher(4,chopsticks[4],chopsticks[0], token);
  
ASSIGN
  --
  -- Initially, all chopsticks are free
  --
  init(chopsticks[0]) := nobody;
  init(chopsticks[1]) := nobody;
  init(chopsticks[2]) := nobody;
  init(chopsticks[3]) := nobody;
  init(chopsticks[4]) := nobody;
  -- initially the token is granted to p0
  init(token) := 0;
    
MODULE philosopher(i,left,right,token)
  
VAR
  state : {thinking, hungry, eating, done};
  
DEFINE
  --
  -- These are just abbreviations, not state variables
  --
  gotleft := (left=i);
  gotright := (right=i);
  leftfree := (left=nobody);
  rightfree := (right=nobody);
    
ASSIGN
  init(state) := thinking;

  next(left)  := case
    (state = done) : nobody; -- drop chopstick
    --
    -- if hungry and left chopstick if free, take it
    --
    (state=hungry) & leftfree & (token=i) : i;
    TRUE : left;
  esac;

  next(right) := case
    (state = done) : nobody; -- drop chopstick
    --
    -- if hungry and have left chopstick and right is free, take it
    --
    (state=hungry) & gotleft & rightfree & (token=i) : i; 
    TRUE : right;
  esac;

  next(state) := case
    (state=thinking) : {thinking, hungry};
    (state=hungry) & gotleft & gotright : eating;
    (state=hungry) : hungry; -- don't have chopsticks
    (state=eating) : {eating, done};
    (state=done) : thinking;
    TRUE : state;
  esac;

  next(token) := case
    (state=thinking) & (token=i) : (token + 1) mod 5; -- pass to the next
    TRUE : token;
  esac;


-- add fairness constraints here
FAIRNESS running;
FAIRNESS state=hungry;

-- add specifications here 

-- safety:

SPEC AG ((state = eating) -> gotleft & gotright)

-- liveness:

SPEC AG ((state = hungry) -> AF (state = eating))
